rule CVE_2021_40444_word__rels
{
    strings:
		$xml="<?xml"
		//suspicious_string
		$ss0="oleObject" nocase
		$ss1="mhtml:http" nocase
		$ss2="!x-usc:http" nocase
	
    condition:
        all of them
}

rule CVE_2021_40444_word_document_xml
{
    strings:
		$ole="<o:OLEObject"
		$ss1="Type=\"Link" nocase
		$ss2="ProgID=\"htmlfile" nocase
		$ss3="UpdateMode=\"OnCall" nocase
		$ss4="EnhancedMetaFile" nocase
		$ss5=/<o:LockedField>.?false.?<\/o:LockedField>/ nocase
		$ss6="\\f 0" nocase
		
    condition:
        all of them
}


rule CVE_2022_30190_word__rels
{
    strings:
		$xml="<?xml"
		//suspicious_string
		$ss0="oleObject" nocase
		$ss1="TargetMode=\"External" nocase
		
    condition:
        all of them
}

rule CVE_2022_30190_word_document_xml
{
    strings:
		$ole="<o:OLEObject"
		$ss1="Type=\"Link" nocase
		$ss2="ProgID=\"htmlfile" nocase
		$ss3="UpdateMode=\"OnCall" nocase
		$ss4="EnhancedMetaFile" nocase
		$ss5=/<o:LockedField>.?false.?<\/o:LockedField>/ nocase
		$ss6="\\f 0" nocase
		
    condition:
        all of them
}


rule CVE_2023_21716
{
    strings:
		$rtf_header={7B 5C 72 74 66}
		//malicious_pattern
		$mp1="\\fonttbl"
		$mp2="crash??"
		$mp3=/\\f[0-9]+A;/
		//\rtlch
		$ms=/\\rt.*lch/
		
    condition:
        $rtf_header at 0 and all of ($mp*,$ms) and #mp3>=32760
}


rule CVE_2023_36884_suspicious_xml_rels
{
    strings:
		$m_rtf=/Target=\"\/word\/[a-zA-Z]+\.rtf\"/ nocase
        $mId="AltChunkId"
		
    condition:
        all of them
}

rule CVE_2023_36884_malicious_rtf
{
    strings:
		$m_ole1_1="objautlink" nocase
		$m_ole1_2="objupdate" nocase
		$m_ole2="Word.Document.8" nocase
        $ms1="68007400740070003A002F002F" nocase //'http://'
		$ms2="5c5c" nocase //'\\'
		
    condition:
        all of them
		and
		@m_ole1_1<@m_ole1_2
}


rule CVE_2023_40477
{//Winrar's version<6.23
    strings:
        $ex_rar=/\.rar/ nocase
		$ex_rev=/\.rev/ nocase
		$ex_rN=/\.r[0-9]{1,3}/ nocase
		
    condition:
        all of them
		and
		//Missing data volume
		for any i in (1..#ex_rN):
			(uint8(@ex_rN[i]+3)-0x30!=(i-1)%10)
		and
		//array bound
		//new style:p[2]+p[0]-1>255
		for any i in (1..#ex_rev):
		(
			uint8(@ex_rev[i]-3)-0x30>=0
			and
			uint8(@ex_rev[i]-3)-0x30<=9
			and
			#ex_rN\2
			+(uint8(@ex_rev[i]-3)-0x30)*100
			+(uint8(@ex_rev[i]-2)-0x30)*10
			+(uint8(@ex_rev[i]-1)-0x30)>255
		)
		/*
		//old style:exist '-'
		or
		for any i in (1..#ex_rev):
		(
			for any j in (2..4):
				(uint8(@ex_rev[i]-j)==0x2D)
		)
		
		*/
}

//---------

rule CVE_2017_11882
{
    strings:
		$rtf_header={7B 5C 72 74 66}
		$equ="Equation.3" nocase
		$equ2="4571756174696f6e2e33" nocase
		$ole=/4f..6c..65/ nocase
		$zz="030101030a0a01085a5a" nocase
		//$payload=/030101030a0a01085a5a[0-9a-zA-Z]{88}142140000000[0-9a-zA-Z]+00/ nocase//only 109b
		/*
		$root=/52..6f..6f..74/ nocase
		$entry=/45..6e..74..72..79/ nocase
		$CompObj=/43..6f..6d..70..4f..62..6a/ nocase
		$System="53797374656d" nocase
		$METAFILEPICT="4d45544146494c4550494354" nocase
		$mathtype="4d61746854797065" nocase
		*/
		
    condition:
        $rtf_header at 0 and
		$ole and $zz and
		($equ or $equ2)
		
}


rule CVE_2018_0798
{
    strings:
		$rtf_header={7B 5C 72 74 66}
		$equ="Equation.3" nocase
		$equ2="4571756174696f6e2e33" nocase
		$ole=/4f..6c..65/ nocase
		$Matrix=/030101030a0a0105010101/ nocase
		
    condition:
        $rtf_header at 0 and
		$ole and $Matrix and
		($equ or $equ2)
		
}


rule CVE_2018_0802
{
    strings:
		$rtf_header={7B 5C 72 74 66}
		$equ="Equation.3" nocase
		$equ2="4571756174696f6e2e33" nocase
		$ole=/4f..6c..65/ nocase
		$space=/(20){30}/
		
    condition:
        $rtf_header at 0 and
		$ole and $space and
		($equ or $equ2)
		
}


rule CVE_2018_20250
{
    strings:
		$ace_header={6B 28 31 00 00 00 90 2A 2A 41 43 45 2A 2A}
		//$path=/C:\\C:C:\.\.\/AppData\\Roaming\\Microsoft\\Windows\\Start Menu\\Programs\\Startup\\hi\.exe/ nocase
		$cpath=/.:\\.:[c-zC-Z]:/ nocase
		//$forward=/\.\./
		/*
		$shell32_dll={53 48 45 4C 4C 33 32 2E 64 6C 6C}
		$KERNEL32_dll={4B 45 52 4E 45 4C 33 32 2E 64 6C 6C}
		$advapi32_dll={41 44 56 41 50 49 33 32 2E 64 6C 6C}
		*/
		
    condition:
        all of them
		
}


rule CVE_2020_9715
{
    strings:
		$pdf_header={25 50 44 46 2D 31 2E}//%PDF-1.
		$mjs1=/this\.dataObjects\[0\]\.toString\(\);/ nocase
		$mjs2=/this\.dataObjects\[0\]\ *=\ *null;/ nocase
		$mjs3=/g_timeout\ *=\ *app\.setTimeOut\(\ *\"[0-9a-zA-Z_]*\(\)\"\ *,\ *1000\ *\);/ nocase
		
    condition:
        all of them and $pdf_header at 0
}


rule Windows_search_ms_defect
{
	strings:
		$script_start=/<script/ nocase
		$script_end=/<\/script>/ nocase
		$wlh=/window\.location\.href/ nocase
		$search_ms=/search-ms/ nocase
		$webdav=/webdav/ nocase
		//$hidden_link=/(src|href)\ *=\ *".*\ hidden/ nocase
		
	condition:
		all of them and
		for any i in (1..#script_start):
		(
			for any j in (1..#wlh):
			(
				@script_start[i]<@wlh[j] and @wlh[j]<@script_end[i] and
				for any k in (1..#search_ms):
				(
					@script_start[i]<@search_ms[k] and @search_ms[k]<@script_end[i] and
					@wlh[j]<@search_ms[k] and
					for all m in (@wlh[j]+!wlh..@search_ms[k]-1):(uint8(m) != 0x3B)
				)
				and
				for any k in (1..#webdav):
				(
					@script_start[i]<@webdav[k] and @webdav[k]<@script_end[i] and
					@wlh[j]<@webdav[k] and
					for all m in (@wlh[j]+!wlh..@webdav[k]-1):(uint8(m) != 0x3B)
				)
			)
		)
		/*
		and
		for any i in (1..#hidden_link):
		(
			for all j in (@hidden_link+3..@hidden_link+!hidden_link):
				(uint8(j) != 0x3C and uint8(j) != 0x3E)
		)
		*/
}

//----------

